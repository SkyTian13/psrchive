#!/bin/false
#-----------------------------------------------------------------------------
# Usage: set psrchive_version=<version> ; source psrchive_version
#-----------------------------------------------------------------------------
# psrchive_version changes the version of psrchive in use.  
# It modifies the PATH and PSRCHIVE_VERSION environment variables.
# The psrchive_version shell variable is unset.
#
# Original: 1999/07/01 by Mark Calabretta
# Adapted and modified: 2007/05/22 by Willem van Straten
#
# $Id: psrchive_version,v 1.3 2007/06/06 05:07:31 straten Exp $
#-----------------------------------------------------------------------------

if (! $?PSRCHIVE_SYSTEM) then
  echo "Error: PSRCHIVE_SYSTEM is not defined."
  exit
endif

if (! $?PSRCHIVE_VERSION) then

  #
  # If the version is not set, assume that this is the first time that
  # this script was sourced and set PSRCHIVE_VERSION to stable
  #

  if (-d $PSRCHIVE_SYSTEM/stable/bin) then

    # Use the current version by default
    setenv PSRCHIVE_VERSION stable

    # Add the system installation directory to the front of the PATH  
    setenv PATH $PSRCHIVE_SYSTEM/stable/bin:$PATH

    # Set up the alias
    alias psrchive_version "set psrchive_version=\!* ;"\
         "source $PSRCHIVE_SYSTEM/stable/share/psrchive_version"

  endif

else

  #
  # If the version is set, then either report it or change it
  #

  # If no version is selected, then report the current version
  if (! $?psrchive_version) set psrchive_version = ""
  if ("$psrchive_version" == "" || "$psrchive_version" == ".") then
     echo "PSRCHIVE System: $PSRCHIVE_SYSTEM Version: $PSRCHIVE_VERSION"
     exit
  endif

  # Check legitimacy of selected version
  if (! -d $PSRCHIVE_SYSTEM/$psrchive_version/bin) then
     echo "Error: $PSRCHIVE_SYSTEM/$psrchive_version does not exist."
     exit
  endif

  # Modify PATH
  setenv PATH `echo $PATH | sed -e "s|$PSRCHIVE_SYSTEM/$PSRCHIVE_VERSION|$PSRCHIVE_SYSTEM/$psrchive_version|"`

  # Redefine PSRCHIVE_VERSION.
  setenv PSRCHIVE_VERSION $psrchive_version

endif

unset psrchive_version

