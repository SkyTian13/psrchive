#!/bin/csh -f

# @configure_input@

#
# This script installs the FFT benchmarks for each FFT library
# 

set prefix=@prefix@
set datarootdir=@datarootdir@

set max_nthread = 1
set ncache = 0

if ( -f /proc/cpuinfo ) then
  set max_nthread = `cat /proc/cpuinfo | grep processor | wc -l`
  set ncache = $max_nthread

  set noglob
  set eval=`grep 'cache size' /proc/cpuinfo | head -1 | awk -F: '{print $2}' | sed -e 's/KB/\* 1024/'`

  set chips=`grep "model name" /proc/cpuinfo | head -1 | awk '{print $7}'`
  echo Chip set: $chips
  if ( $chips == E5520 ) then
    echo Nehalem Level 3 cache shared by 4 cores
    set ncache = 2
  endif
  if ( $chips == E5345 ) then
    echo Clovertown Level 2 cache shared by 2 cores
    set ncache = 4
  endif

  @ cache = $eval / 2
  unset noglob
else
  set max_nthread = `sysctl hw.ncpu | awk '{print $2}'`
endif

echo Maximum number of threads = $max_nthread
echo Streaming cache size = $ncache X $cache

foreach lib ( $* )

  set nthread = 1
  while ($nthread <= $max_nthread)

    set use_cache = $cache
    if ( $ncache != 0 && $nthread > $ncache ) then
      @ use_cache = ($cache * $ncache) / $nthread
    endif

    echo Threads: $nthread Cache: $use_cache

    @srcdir@/fft_bench --library=$lib --nthread=$nthread --cache=$use_cache
    mv fft_bench.out @datadir@/fft_bench_${lib}_${nthread}.dat

    @ nthread = $nthread * 2

  end

end

